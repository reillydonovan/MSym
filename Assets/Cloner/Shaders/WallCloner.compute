// Cloner - An example of use of procedural instancing.
// https://github.com/keijiro/Cloner

#pragma kernel ClonerUpdate

#include "Common.hlsl"
#include "SimplexNoise2D.hlsl"

RWStructuredBuffer<float4> TransformBuffer;

CBUFFER_START(Params)

    uint BufferStride;

    uint2 Iteration;
    float2 Extent;
    float2 Scroll;

    float PositionNoise;
    float NormalNoise;

    float BaseScale;
    float ScaleNoise;
    float ScalePulse;

    float NoiseFrequency;
    float2 NoiseOffset;
    float PulseProbability;
    float PulseTime;

CBUFFER_END

[numthreads(128, 1, 1)]
void ClonerUpdate(uint id : SV_DispatchThreadID)
{
    if (id >= Iteration.x * Iteration.y) return;

    // Normalized position
    float2 p_01 = float2(id % Iteration.x, id / Iteration.x) / Iteration;

    // Wrap-around count
    int2 wc = floor(p_01 + Scroll * 0.5 / Extent);

    // ID with wrapping around
    int id_w = id + wc.x * Iteration.x + wc.y * Iteration.y;

    // Original position with wrapping around
    float2 p_w = ((p_01 - wc) * 2 - 1) * Extent;

    // Noise field x2
    float3 sn1 = snoise_grad(98.4898 + p_w * NoiseFrequency + NoiseOffset);
    float3 sn2 = snoise_grad( 1.2278 - p_w * NoiseFrequency + NoiseOffset);

    // Normal modification
    float3 nz = normalize(float3(sn1.xy * NormalNoise, 1));
    float3 ny = normalize(cross(nz, float3(1, 0, 0)));
    float3 nx = cross(nz, ny);

    // UV offsetting
    uint hash = Hash(id_w);
    float uoffs = float((hash >> 24) & 0xfff) / 0x1000;
    float voffs = float( hash        & 0xfff) / 0x1000;
    float uvoffs = uoffs + floor(voffs * 1000);

    // Pulse noise
    float ptime = PulseTime + float(hash & 0xfffff) / 0x100000;
    uint seed = (uint)(id_w * 131071) + (uint)ptime;
    float pulse0 = Random(seed + 0) < PulseProbability;
    float pulse1 = Random(seed + 1) < PulseProbability;
    float pulse = lerp(pulse0, pulse1, smoothstep(0, 1, frac(ptime)));

    // Scale factor
    float s_n = max(0, 1 + sn1.z * ScaleNoise) * BaseScale;
    float s_p = pulse * ScalePulse * BaseScale;

    // Position modification
    float3 disp = (nx * sn2.x + ny * sn2.y) * 0.2 + nz * sn2.z;
    float3 p = float3(p_w + Scroll, 0) + disp * s_n * PositionNoise;

    TransformBuffer[id + BufferStride * 0] = float4(p, s_n + s_p);
    TransformBuffer[id + BufferStride * 1] = float4(nx, uvoffs);
    TransformBuffer[id + BufferStride * 2] = float4(ny, sn2.z);
}
